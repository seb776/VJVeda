// This work is licensed under the Creative Commons Attribution-NonCommercial-ShareAlike 3.0
// Unported License. To view a copy of this license, visit http://creativecommons.org/licenses/by-nc-sa/3.0/ 
// or send a letter to Creative Commons, PO Box 1866, Mountain View, CA 94042, USA.
// =========================================================================================================

float seed;
float rand()
{
    seed++;
    return hash11(seed);
}

mat2 r2d(float a) { float c = cos(a), s = sin(a); return mat2(c, -s, s, c); }

vec3 getCam(vec3 rd, vec2 uv)
{
    vec3 r = normalize(cross(rd, vec3(0.,1.,0.)));
    vec3 u = normalize(cross(rd, r));
    return normalize(rd+r*uv.x+u*uv.y);
}
float _sqr(vec2 p, vec2 s)
{
    vec2 l = abs(p)-s;
    return max(l.x, l.y);
}
vec2 _min(vec2 a, vec2 b)
{
    if (a.x < b.x)
        return a;
    return b;
}

vec2 map(vec3 p)
{
    vec2 acc = vec2(10000.,-1.);
    
    //acc = _min(acc, vec2(length(p-vec3(sin(iTime*2.)-.5,cos(iTime*1.7)+cos(iTime*.5)*.5,cos(iTime*3.)+8.))-.5, 2.));
    
    acc = _min(acc, vec2(-(abs(p.y)-2.8), 0.));
    vec3 pwall = p;
    //pwall.xy*=r2d(.5);
    float sidewall = -(abs(p.y+p.x)-3.8);
    
    sidewall = max(sidewall, -(abs(p.z-3.5)-1.));
    acc = _min(acc, vec2(sidewall, 0.)); // Sidewalls
    acc = _min(acc, vec2(-(abs(p.z)-12.), 1.));
    
    return acc;
}

vec3 getNorm(vec3 p, float d)
{
    vec2 e = vec2(0.01,0.);
    return normalize(vec3(d)-
        vec3(map(p-e.xyy).x, map(p-e.yxy).x, map(p-e.yyx).x));
}

vec3 trace(vec3 ro, vec3 rd, int steps)
{
    vec3 p = ro;
    for (int i = 0; i < steps; ++i)
    {
        vec2 res = map(p);
        if (res.x < 0.01)
            return vec3(res.x, distance(p, ro), res.y);
        p+= rd*res.x;
    }
    return vec3(-1.);
}

vec3 getMat(vec3 rd, vec3 res, vec3 p, vec3 n)
{
    vec3 col = vec3(0.);
    
    col = n*.5+.5;
    if (res.z == 0.)
    {
        if (abs(dot(n,vec3(0.,0.,1.))) < 0.01)
        {
            col = vec3(0.051,0.404,0.408)*.2*pow(texture(iChannel0, p.xz*.1).x, .1)*vec3(.5); // ambient
        }
        else
            col = vec3(0.);
    }
    if (res.z == 1.)
    {
        vec2 uvwall = p.xy;
        float rep = 3.;
        float idwin = floor((uvwall.x+rep*.5)/rep);
        uvwall.y = abs(uvwall.y);
        uvwall.y -= 1.8;
        uvwall.x = mod(uvwall.x+rep*.5,rep)-rep*.5;
        float sqr = _sqr(uvwall, vec2(1.4, .78));
        float wincolfactor = sat(sin(idwin*1.+3.*iTime*sign(p.y))*.5+.5);
        //wincolfactor += pow(FFT(.1),2.);
        wincolfactor *= (mod(iTime, .2)/.2)*FFT(.1);
        vec3 rgbwin = mix(vec3(1.), vec3(1.,0.1,0.4), wincolfactor);
        col = mix(vec3(0.), rgbwin*pow(FFT(.9),.5), 1.-sat(sqr*40.));
    }
    if (res.z == 2.)
    {
        col = vec3(0.165,0.996,0.678)*10.*FFT(.4);
    }
    
    return col;
}
float getKeyPress(int keycode)
{
    return texelFetch( iChannel1, ivec2(keycode,1),0 ).x;
}
float getKeyState(int keycode)
{
    return texelFetch( iChannel1, ivec2(keycode,0),0 ).x;
}
float getWallSpec(vec3 p)
{
    return pow(texture(iChannel0, p.xz*.1).x, .1);
}

vec3 rdr(vec2 uv)
{
    vec3 col = vec3(0.);
    
    vec3 ro = vec3(-2.+sin(iTime*.2),1.+sin(iTime*.25),-8.);
    vec3 ta = vec3(-2.+sin(iTime*.2),0.+sin(iTime*.3),0.);
    vec3 rd = normalize(ta-ro);
    rd = getCam(rd, uv);
    
    vec3 res = trace(ro, rd, 128);
    if (res.y > 0.)
    {
        vec3 p = ro+rd*res.y;
        vec3 n = getNorm(p, res.x);
        col = getMat(rd, res, p, n);
        
        if (res.z == 0.)
        {
            if (abs(dot(n,vec3(0.,0.,1.))) < 0.01)
            {
                float spec = pow(texture(iChannel0, p.xz*.1).x, 1.)*.2;
                vec3 refl = normalize(reflect(rd, n)+spec*2.*(vec3(rand(), rand(), rand())-.5));
                vec3 resrefl = trace(p+n*0.01, refl, 128);
                if (resrefl.y > 0.)
                {
                    vec3 prefl = p+refl*resrefl.y;
                    vec3 nrefl = getNorm(prefl, resrefl.x);
                    col += getMat(refl, resrefl, prefl, nrefl);
                }
            }
        }
        
    }
    if (getKeyState(KEY_LEFT) > .1)
    col *= sat(sin(iTime*80.));
    return col;
}

void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
    vec2 uv = (fragCoord-.5*iResolution.xy)/iResolution.xx;
    seed=texture(iChannel0,uv).x;
    seed+=fract(iTime);
    vec3 col = rdr(uv);
    
    col = mix(col, texture(iChannel2, fragCoord/iResolution.xy).xyz, .7);
    fragColor = vec4(col,1.0);
}